Imports System.Drawing
Imports MahjongGK.Contracts.GlobalEnum

Friend Module ResLichtkarteKopiervorlage

    Private _imageLichtkarteOriginal As Bitmap = Nothing

    Private _imageLichtkarteSpiel As Bitmap = Nothing
    Private _imageLichtkarteSpielSize As Size = Size.Empty

    Private _imageLichtkarteEdit As Bitmap = Nothing
    Private _imageLichtkarteEditSize As Size = Size.Empty

    '
    ''' <summary>
    ''' Liefert die auf die aktuelle Steingröße heruntergerechnete LightMap.
    ''' Besitzer der zurückgegebenen Bitmap ist das Ressourcenmodul.
    ''' Die Bitmap darf von außen nicht disposed werden.
    ''' </summary>
    Public Function Image_LichtkarteKopiervorlage(request As TileRequest) As Bitmap

        'Hinweis: DisposeLichtkarten_Kopiervorlage auch anpassen (steht gleich im Anschluss an diese Funktion)
        'Beides auch in ResLichtkarten eintragen. 
        'Der Base64Code aus dem HPgrLightMapErstellung wird hier im Modul ganz am Schluss eingefügt
        '(Im Modul ist bewußt doppelter Code drin, da das Modul völlig eigenständig arbeitet.)
        '############################################################################################

        Select Case request.AktRenderMode
            Case AktRenderMode.Spiel
                Dim targetSize As Size = TileFactoryAPI.GetAktLayout(AktRenderMode.Spiel).FaceOuterRect.Size
                If _imageLichtkarteSpiel Is Nothing OrElse _imageLichtkarteSpielSize <> targetSize Then
                    ReplaceScaledCache(_imageLichtkarteSpiel, _imageLichtkarteSpielSize, targetSize)
                    _imageLichtkarteSpiel = CreateScaledLightMap(targetSize)
                End If

                Return _imageLichtkarteSpiel

            Case AktRenderMode.Edit
                Dim targetSize As Size = TileFactoryAPI.GetAktLayout(AktRenderMode.Edit).FaceOuterRect.Size
                If _imageLichtkarteEdit Is Nothing OrElse _imageLichtkarteEditSize <> targetSize Then
                    ReplaceScaledCache(_imageLichtkarteEdit, _imageLichtkarteEditSize, targetSize)
                    _imageLichtkarteEdit = CreateScaledLightMap(targetSize)
                End If

                Return _imageLichtkarteEdit

            Case AktRenderMode.None
                Dim layout As TileLayout = TileFactoryAPI.GetLayout(request.SteinSize, request.SteinBasisSize)

                Return CreateScaledLightMap(layout.FaceOuterRect.Size)

            Case Else

                Return Nothing 'wird nicht erreicht
        End Select

    End Function

    '
    ''' <summary>
    ''' Räumt alle vom Modul gehaltenen Bitmaps auf.
    ''' </summary>
    Public Sub DisposeLichtkarten_Kopiervorlage()

        DisposeBitmap(_imageLichtkarteSpiel)
        _imageLichtkarteSpielSize = Size.Empty

        DisposeBitmap(_imageLichtkarteEdit)
        _imageLichtkarteEditSize = Size.Empty

        DisposeBitmap(_imageLichtkarteOriginal)

    End Sub

    Private Sub ReplaceScaledCache(ByRef cacheBitmap As Bitmap,
                                   ByRef cacheSize As Size,
                                   newSize As Size)

        DisposeBitmap(cacheBitmap)
        cacheSize = newSize

    End Sub

    Private Function CreateScaledLightMap(targetSize As Size) As Bitmap

        Dim source As Bitmap = GetOriginalLightMap()

        If source.Width = targetSize.Width AndAlso source.Height = targetSize.Height Then
            Return New Bitmap(source)
        End If

        Return ScaleBitmapTwoStage(source, targetSize)

    End Function

    Private Function GetOriginalLightMap() As Bitmap

        If _imageLichtkarteOriginal Is Nothing Then
            _imageLichtkarteOriginal = LoadImageLichtkarte()
        End If

        Return _imageLichtkarteOriginal

    End Function

    Private Function ScaleBitmapTwoStage(source As Bitmap,
                                         targetSize As Size) As Bitmap

        Dim current As New Bitmap(source)

        Try
            Do While targetSize.Width <= current.Width \ 2 AndAlso
                     targetSize.Height <= current.Height \ 2

                Dim nextSize As New Size(
                    Math.Max(targetSize.Width, current.Width \ 2),
                    Math.Max(targetSize.Height, current.Height \ 2))

                Dim nextBmp As Bitmap = ScaleBitmapHighQuality(current, nextSize)
                current.Dispose()
                current = nextBmp
            Loop

            If current.Width <> targetSize.Width OrElse current.Height <> targetSize.Height Then
                Dim finalBmp As Bitmap = ScaleBitmapHighQuality(current, targetSize)
                current.Dispose()
                current = finalBmp
            End If

            Return current

        Catch
            current.Dispose()
            Throw
        End Try

    End Function

    Private Function ScaleBitmapHighQuality(source As Bitmap,
                                            targetSize As Size) As Bitmap

        Dim result As New Bitmap(targetSize.Width, targetSize.Height, Imaging.PixelFormat.Format32bppArgb)

        Using g As Graphics = Graphics.FromImage(result)
            g.Clear(Color.Transparent)
            SetGraphicsMode(g, sourceOver:=False)

            g.DrawImage(source, New Rectangle(0, 0, targetSize.Width, targetSize.Height))

        End Using

        Return result

    End Function

    Private Sub DisposeBitmap(ByRef bmp As Bitmap)

        If bmp IsNot Nothing Then
            bmp.Dispose()
            bmp = Nothing
        End If

    End Sub

    '###############################################
    'ab hier aus dem HPgrLightMapErstellung Code aus der Zwischenablage einfügen

    Private Function LoadImageLichtkarte() As Image
        Return Nothing
    End Function

End Module
